Option Explicit

Sub auto_table_contents()

Dim StartCell As Range 'for input box to select range
Dim EndCell As Range 'for message box as info
Dim Sh As Worksheet
Dim ShName As String
Dim MsgConfirm As VBA.VbMsgBoxResult 'for msgbox to confirm

On Error Resume Next

Set StartCell = Excel.Application.InputBox("Where do you want to insert the table of content?" _
& vbNewLine & "Please select a cell:", "Insert table of content", , , , , , 8)

If Err.Number = 424 Then Exit Sub
On Error GoTo Handle

'set only one cell
Set StartCell = StartCell.Cells(1, 1)
Set EndCell = StartCell.Offset(Worksheets.Count - 2, 1)

'Get confirmation
MsgConfirm = VBA.MsgBox("the value in the cells" & vbNewLine & StartCell.Address & " to " & EndCell.Address _
& " could be overwritten." & vbNewLine & "Do you want to continue?", vbOKCancel + vbDefaultButton2, "Confirmation Required")
If MsgConfirm = vbCancel Then Exit Sub


For Each Sh In Worksheets
ShName = Sh.Name
StartCell = ShName
    If ActiveSheet.Name <> ShName Then
        If Sh.Visible = xlSheetVisible Then
            ActiveSheet.Hyperlinks.Add Anchor:=StartCell, Address:="", SubAddress:= _
                "'" & ShName & "'!A1", TextToDisplay:=ShName
            StartCell.Offset(0, 1).Value = Sh.Range("A1").Value
            Set StartCell = StartCell.Offset(1, 0)
        End If
    End If
Next Sh
Exit Sub
Handle:
MsgBox "Unfortunately, an error has occured."

End Sub


